# Wang-Buzsaki neuron adapted from Komek et al. 2012
# and projected into 2d

#
# Parameters used
p gKLe=0.12, gNaL=0.017, gKLi=0.15
p gK=9.0, gNa=35.0
p ENa=55.0, EK=-90.0
p phi=5.0, angle = -1.3057
p Vrest = -71.962
#
# Tonic input description and parameters
p Iapp=0
#
#ODEs
Ve'=Iapp-gKLe*(Ve-EK)-gNaL*(Ve-ENa)-gNa*(Minf(ve)^3)*hnew(z1)*(Ve-ENa)-gK*((nnew(z1))^4)*(Ve-EK)
z1'= -cos(angle)*(z1*cos(angle) + Ninf(Vrest)-Ninf(Ve))/tauN(Ve) -sin(angle)*(z1*sin(angle) + Hinf(Vrest) - Hinf(Ve))/tauH(Ve)
#Ve'=Iapp-gKLe*(Ve-EK)-gNaL*(Ve-ENa)-gNa*(Minf(ve)^3)*he*(Ve-ENa)-gK*(ne^4)*(Ve-EK)
#he'=phi*(Hinf(ve)-he)/tauH(ve)
#ne'=phi*(Ninf(ve)-ne)/tauN(ve)
#
# functions
alpham(v)=0.1*(V+35.0)/(1.0-exp(-(V+35.0)/10.0))
betam(v)=4.0*exp(-(V+60.0)/18.0)
Minf(v)=alpham(v)/(alpham(v)+betam(v))
#
alphah(v)= 0.07*exp(-(V+58.0)/20.0)
betah(v)=1.0/(1.0+exp(-(V+28.0)/10.0))
Hinf(v)=alphah(v)/(alphah(v)+betah(v))
tauH(v)=1.0/(phi*(alphah(v)+betah(v)))
#
alphan(v)=0.01*(V+34.0)/(1.0-exp(-(V+34.0)/10.00))
betan(v)=0.125*exp(-(V+44.0)/80.0)
Ninf(v)=alphan(v)/(alphan(v)+betan(v))
tauN(v)=1.0/(phi*(alphan(v)+betan(v)))
#
hnew(z1) = Hinf(Vrest) + z1*sin(angle)
nnew(z1) = Ninf(Vrest) + z1*cos(angle)
# Initial conditions
init Ve=-64
init z1 = 0
#init he=0.78
#init ne=0.09
#
#
# Numerics description
@ XP=Ve
@ YP=Z1
@ autoeval=0
@ TOTAL=100
@ nOut=10  
@ DT=0.01,bound=100000,maxstor=1000000
@ METH=euler
@ TOLER=0.00001
@ XLO=-100, XHI=60.0, YLO=-2.0, YHI=2.0
done